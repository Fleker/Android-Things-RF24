apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version rootProject.ext.version
group 'com.bintray'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/libRF24-release.aar")
            groupId 'nz.org.winters'
            artifactId 'libRF24'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_KEY') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'Android-Things-RF24'
        description = "Android things port of RF24 and RF24 Network"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/wintersandroid/Android-Things-RF24.git'
        dryRun = true
        version {
            name = this.version
            desc = "Android-Things-RF24 ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}